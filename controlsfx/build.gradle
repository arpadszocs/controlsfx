apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'osgi'
apply plugin: 'maven'

apply from: 'mavenPublish.gradle'

repositories {
    mavenCentral()
}

dependencies {
    compile localGroovy()
    compile('org.codehaus.groovy.modules.http-builder:http-builder:0.7.1') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
        exclude group: 'org.codehaus.groovy', module: 'groovy'
    }
}

Properties commons = new Properties()
commons.load(new FileInputStream("$rootDir/../controlsfx-build.properties"))

group = commons.controlsfx_group
version = commons.controlsfx_version

ext {
    specification_version = commons.controlsfx_specification_version
    specification_title = commons.controlsfx_specification_title
    implementation_version = commons.controlsfx_version
    transifex_username = ""
    transifex_password = ""
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

configurations {
    jdk
    
    if (project.hasProperty("transifex.username")) {
        transifex_username = project.property("transifex.username")
    }
    if (project.hasProperty("transifex.password")) {
        transifex_password = project.getProperty("transifex.password")
    }
}

sourceSets {
    main {
        compileClasspath += configurations.jdk
    }
}

dependencies {
    try {
        jdk files(jfxrtJar)
    } catch (MissingPropertyException pne) {
        // javafx plugin will provide in this case
    }
}

task downloadTranslations << {
    description = "Download translations from Transifex"
    
    if (transifex_username.equals("") || transifex_password.equals("")) {
        logger.warn "----------------------------------------------------------"
        logger.warn "Cannot determine Transifex Username/Password."
        logger.warn "If you want to build ControlsFX with its translations then"
        logger.warn "you need to create a Transifex account and set"
        logger.warn "transifex.username & transifex.password properties in your" 
        logger.warn "gradle.properties file." 
        logger.warn "----------------------------------------------------------"
    } else {
        javaexec {
            main = 'impl.org.controlsfx.transifex.Transifex'
            classpath = sourceSets.main.runtimeClasspath
            systemProperty 'transifex.username', transifex_username
            systemProperty 'transifex.password', transifex_password
        }
    }
}

processResources.finalizedBy(downloadTranslations)

task compileCSS << {
    ant.delete (includeEmptyDirs: 'true') { 
        fileset(dir: file("build/resources/main"), includes: "**/*.bss") 
    }
    javaexec {
        main = "com.sun.javafx.tools.packager.Main"
        classpath = files("${System.properties['java.home']}/../lib/ant-javafx.jar")
        args = [ "-createbss",
            "-srcdir", "src/main/resources",
            "-outdir", "build/resources/main"
        ]
    }
}

processResources.finalizedBy(compileCSS)

compileJava {  
    options.encoding = "UTF-8"
}

javadoc {
    exclude 'impl/*'
    failOnError = true
    classpath = project.sourceSets.main.runtimeClasspath + configurations.jdk
    
    options.windowTitle("ControlsFX Project ${version}")
    options.links("http://docs.oracle.com/javase/8/docs/api/");
    options.links("http://docs.oracle.com/javase/8/javafx/api/");
    options.addBooleanOption("Xdoclint:none").setValue(true);
    options.overview("src/main/docs/overview.html");
    
    // All doc-files are located in src/main/docs because Gradle's javadoc doesn't copy
    // over the doc-files if they are embedded with the sources. I find this arrangement
    // somewhat cleaner anyway (never was a fan of mixing javadoc files with the sources)
    doLast {
        copy {
            from "src/main/docs"
            into "$buildDir/docs/javadoc"
        }
    }
}

jar {
    //exclude '**/16/*'
    exclude '**/32/*'
    exclude '**/64/*'
    exclude '**/128/*'
    exclude '**/oxygen/svg/*'
    exclude 'impl/org/controlsfx/transifex/**'

    manifest { // the manifest of the default jar is of type OsgiManifest
        attributes (\
            'Specification-Title': specification_title,\
            'Specification-Version': specification_version,\
            'Implementation-Title': 'ControlsFX',\
            'Implementation-Version': implementation_version,\
            'Bundle-Name': 'ControlsFX'
        )
        instruction 'Bundle-Description', 'High quality UI controls and other tools to complement the core JavaFX distribution'        
        instruction 'Export-Package',
                '!impl.org.controlsfx.*',
                'org.controlsfx.*'
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.java
    from sourceSets.main.resources
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    dependsOn javadoc
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.8'
}

artifacts {
    archives sourceJar
    archives javadocJar
}

