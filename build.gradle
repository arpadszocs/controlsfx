apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'application'

apply from: 'mavenPublish.gradle'

group = 'org.controlsfx'
version = '8.0.3-SNAPSHOT'

applicationName = 'FXSampler'
mainClassName = "fxsampler.FXSampler"
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

configurations {
    samples.extendsFrom mainRuntime
    jdk
}

sourceSets {
    main {
        compileClasspath += configurations.jdk
    }
    samples {
        java {
            compileClasspath += main.output
            compileClasspath += configurations.jdk
            compileClasspath += configurations.compile
            runtimeClasspath += samples.output
            runtimeClasspath += main.output
            runtimeClasspath += configurations.jdk
            runtimeClasspath += javadocJar.output
            runtimeClasspath += configurations.runtime
        }
    }
}

dependencies {
    runtime "fxsampler:fxsampler:1.0.0-SNAPSHOT"
    compile "fxsampler:fxsampler:1.0.0-SNAPSHOT"
    try {
        jdk files(jfxrtJar)
    } catch (MissingPropertyException pne) {
        jdk files("${System.properties['java.home']}/lib/jfxrt.jar")
        jdk files("${System.properties['java.home']}/lib/ext/jfxrt.jar")
    }
}

// Samples should be on the class path to run FXSampler
run {
  classpath += sourceSets.samples.output
}

javadoc {
    exclude 'impl/*'
    failOnError = false
    classpath = project.sourceSets.main.runtimeClasspath + configurations.jdk
    
    options.windowTitle("ControlsFX Project ${version}")
    options.links("http://download.oracle.com/javase/7/docs/api/");
    options.links("http://docs.oracle.com/javafx/2/api/");
    options.addBooleanOption("Xdoclint:none").setValue(true);
    options.overview("src/main/docs/overview.html");
    
    // All doc-files are located in src/main/docs because Gradle's javadoc doesn't copy
    // over the doc-files if they are embedded with the sources. I find this arrangement
    // somewhat cleaner anyway (never was a fan of mixing javadoc files with the sources)
    doLast {
        copy {
            from "src/main/docs"
            into "$buildDir/docs/javadoc"
        }
    }
}

jar {
    //exclude '**/16/*'
    exclude '**/32/*'
    exclude '**/64/*'
    exclude '**/128/*'
    exclude '**/oxygen/svg/*'
}

task jarSamples (type: Jar) {
    classifier='samples'
    from sourceSets.samples.output
    manifest {
        attributes(
            'Manifest-Version': '1.0',
            "Main-Class": "org.controlsfx.HelloControlsFX",
            "Class-Path": "controlsfx-${version}.jar"
        )
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.java
    from sourceSets.main.resources
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    dependsOn javadoc
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.8'
}

artifacts {
    archives sourceJar
    archives javadocJar
    archives jarSamples
}

task listJars << {
    configurations.compile.each { File file -> println file.name }
}


