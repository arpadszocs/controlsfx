apply plugin: 'java'
apply plugin: 'maven'

group = 'org.controlsfx'
version = '8.0.0-developer-preview-1'

configurations {
    samples.extendsFrom mainRuntime
    jdk
}

sourceSets {
    main {
        compileClasspath += configurations.jdk
    }
    samples {
        java {
            compileClasspath += main.output
            compileClasspath += configurations.jdk
        }
    }
}

dependencies {
    try {
        jdk files(jfxrtJar)
    } catch (MissingPropertyException pne) {
        jdk files("${System.properties['java.home']}/lib/jfxrt.jar")
        jdk files("${System.properties['java.home']}/lib/ext/jfxrt.jar")
    }
}

javadoc {
    exclude 'impl/*'
    failOnError = false
    classpath = project.sourceSets.main.compileClasspath + configurations.jdk
}

jar {
    exclude '**/16/*
    exclude '**/32/*'
    exclude '**/64/*'
    exclude '**/128/*'
}

task jarSamples (type: Jar) {
    classifier='samples'
    from sourceSets.samples.output
    manifest {
        attributes(
            'Main-Class': 'org.controlsfx.HelloControlsFX'
        )
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.java
    from sourceSets.main.resources
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    dependsOn javadoc
    from javadoc.destinationDir
    classifier = 'javadoc'
}

artifacts {
    archives sourceJar
    archives javadocJar
    archives jarSamples
}

['HelloControlsFX'/*, 'HelloGridView', 'HelloRangeSlider', 'HelloRating', 'HelloSegmentedButton', 'HelloToggleSwitch', 'SVGTest'*/].each { name ->
    task "$name" (type: JavaExec) {
        main = "org.controlsfx.$name"
        classpath = sourceSets.samples.runtimeClasspath + sourceSets.main.runtimeClasspath + configurations.jdk
    }
}