apply plugin: 'java'
apply plugin: 'maven'

group = 'org.controlsfx'
version = '8.0.0-developer-preview-1'

sourceSets {
    samples {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
        }
    }
}

configurations {
    samples.extendsFrom mainRuntime
}

dependencies {
    try {
        compile files(jfxrtJar)
    } catch (MissingPropertyException pne) {
        compile files("${System.properties['java.home']}/lib/ext/jfxrt.jar")
    }
}

task jarSamples (type: Jar) {
    classifier='samples'
    from sourceSets.samples.output
}

artifacts {
    archives jar
    archives jarSamples
}

['HelloDialog', 'HelloGridView', 'HelloRangeSlider', 'HelloRating', 'HelloSegmentedButton', 'HelloToggleSwitch', 'SVGTest'].each { name ->
    task "$name" (type: JavaExec) {
        main = "org.controlsfx.samples.$name"
        classpath = sourceSets.samples.runtimeClasspath
    }
}